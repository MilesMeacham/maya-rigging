/*
Transfer Animation Tool

Instructions: 
-Select source animation object and run code
-Select the objects to which you want to transfer the animation
-Choose Frame Offset and Randomization values
*/



//close window when trying to open new window to avoid duplicates
if (`window -ex win_pasteAnim`){
    deleteUI -window win_pasteAnim;
}

//create a window to contain tool and add buttons and fields
window -t "Transfer Animation Tool" -wh 500 120 -bgc 0.3 0.3 0.3 win_pasteAnim;
columnLayout -adj true;
textFieldGrp -l "Source Object" -editable false -tx "Need to Copy Animation" tx_src;
intFieldGrp -l "Frame Offset Amount" -v1 0 int_offset;
floatFieldGrp -l "Randomize" -v1 0 fl_rand;
button -l "Copy Animation" -c "copyAnimKeys" -bgc 0.1 0.1 0.1 btn0;
button -l "Paste Animation" -c "pasteAnimKeys" -bgc 0.1 0.1 0.1 btn1;
button -l "Close Window" -c "deleteUI -window win_pasteAnim" -bgc 0.1 0.1 0.1 btn2;
showWindow win_pasteAnim;
    
    
global proc copyAnimKeys(){
    
    //select the animated object
    string $sel[] = `ls -sl`;
    

    //check that only one source object is selected
    if (size($sel) != 1){
        warning "Must select one animated object";
    }else{
        copyKey -time ":";
    }
    //$sel[0]
    
}
    
    //This Global Procedure will copy the animation to the selected objects
    global proc pasteAnimKeys(){
        int $offsetTime = `intFieldGrp -q -v1 int_offset`;
        float $randVal = `floatFieldGrp -q -v1 fl_rand`;
        string $selObj_pasteKeys[] = `ls -sl`;
        
        //for loop to run through all selected objects and paste the animation
        for ($objQuant = 0; $objQuant < size($selObj_pasteKeys); $objQuant++){
            int $currentTime = `currentTime -q`;
            int $time = $currentTime + $offsetTime;
            
            //if randomize is zero, just transfer the animation with the offset
            if($randVal == 0){

                currentTime $time;
                select $selObj_pasteKeys[$objQuant];
                pasteKey -time $time -float 1 -option merge -copies 1 -timeOffset 0 -floatOffset 0 -valueOffset 0;
                
            //if there is a randomization then transfer the animation with offset and randomization
            }else{
                
                float $randomVal = rand($time, $randVal);
                currentTime $time;
                select $selObj_pasteKeys[$objQuant];
                pasteKey -time $randomVal -float 1 -option merge -copies 1 -timeOffset 0 -floatOffset 0 -valueOffset 0; 
            }
        }
    }
