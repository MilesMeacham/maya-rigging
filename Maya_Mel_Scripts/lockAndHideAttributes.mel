/*
Lock and Hide Attributes Tool
-This tool will allow you to lock and hide
 (or unlock and show) attributes of selected
 objects.
*/

//open the window
drawUI();

//This procedure defines what the window will look like
global proc drawUI(){
    //close the window if it is already open as to not open multiple
    if (`window -ex win_LockAttribute`){
        deleteUI -window win_LockAttribute;
        drawUI();
    } else {
        window -t "Lock and Hide Attributes" -wh 500 120 -rtf on -bgc 0.25 0.25 0.25 win_LockAttribute;
        columnLayout -adj true;
        radioButtonGrp -numberOfRadioButtons 2 -l "Attibutes: " -labelArray2 "Lock" "Unlock" radio1;
        radioButtonGrp -numberOfRadioButtons 2 -l " " -labelArray2 "Hide" "Show" radio2;
        checkBoxGrp -numberOfCheckBoxes 4 -label "What: " 
                    -labelArray4 "Translate" "Rotate" "Scale" "Visibility" whatToLock;
        button -l "Apply" -c "lockAndHide" -w 100 -bgc 0.35 0.35 0.35 btn0;
        button -l "Close Window" -c "deleteUI -window win_LockAttribute" -bgc 0.35 0.35 0.35 btn1;
        showWindow win_LockAttribute;
    }
}


//This is the main procedure of the script
//It will lock and hide according to what 
//the user has selected
global proc lockAndHide(){
    
    //assign all selected objects to a string array
    string $sel[] = `ls -sl`;
    
    //this for loop will go through each object in the selection
    //and lock and hide the attributes according to the user input
    for( $i = 0; $i < size($sel); $i++){
        
        //declaring variables and querying the fields in the window
        string $name = $sel[$i];
        $lockAttributes = (`radioButtonGrp -q -sl radio1` -1);
        $hideAttributes = (`radioButtonGrp -q -sl radio2` -1);
        $translate = `checkBoxGrp -q -v1 whatToLock`;
        $rotate = `checkBoxGrp -q -v2 whatToLock`;
        $scale = `checkBoxGrp -q -v3 whatToLock`;
        $visibility = `checkBoxGrp -q -v4 whatToLock`;
   
        
        //I made it so "Unlock" was above "Show" in the window
        //and because I did that, I now need to reverse the
        //"lockAttributes" variable because it is backwards.
        if($lockAttributes == true){
            $lockAttributes = false;
        } else {
            $lockAttributes = true;
        }
        
        //translate attributes
        if($translate == true){
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".tx");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".ty");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".tz");
        }
        
        //rotate attributes
        if($rotate == true){
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".rx");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".ry");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".rz");
        }
        
        //scale attributes
        if($scale == true){
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".sx");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".sy");
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".sz");
        }
        
        //visibility attributes
        if($visibility == true){
            setAttr -lock $lockAttributes -keyable $hideAttributes -channelBox false ($name + ".v");
        }
    } //for loop end
} //proc end

